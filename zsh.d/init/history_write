ZSH_HISTORY_FILE="$HOME/.zsh_history"
HISTORY_IGNORE="(history*|cd*|l[sal]?(*)|tree|exit*|clear|reset)"

# BEGIN: zsh history skip original history write
function zshaddhistory() {

  # Set command line variable to use in save_command
  cmd=${1%%$'\n'}
  return 1
}
# END: zsh history skip original history write

# BEGIN: zsh history write on condition
function save_command() {

  STATUS_command=$?

  # Do not save command if it is in HISTORY_IGNORE
  # or if it is too short
  if [[ "$cmd" == ${~HISTORY_IGNORE} || ${#cmd} -le 3 ]]; then
    return 1
  fi

  if (( $STATUS_command == 0 )); then
    print -sr -- "$cmd"
    fc -W
  fi
}
# END: zsh history write on condition

# When the command status is available in precmd not in zshaddhistory.
autoload -Uz add-zsh-hook
# zshaddhistory() is called before precmd.
add-zsh-hook precmd save_command
