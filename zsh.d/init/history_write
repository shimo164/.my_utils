HISTORY_IGNORE="(history*|cd*|l[sal]?(*)|exit*)"
MIN_CMD_LENGTH_TO_SAVE=6

function is_ignore_history() {

  # Ignore if it matches HISTORY_IGNORE
  if [[ "$cmd" == ${~HISTORY_IGNORE} ]]; then
    return 0
  fi

  # Ignore if too short
  if (( ${#cmd} < MIN_CMD_LENGTH_TO_SAVE )); then
    return 0
  fi

  return 1
}

# zsh history skip original history write
function zshaddhistory() {

  # Set command line as the global variable
  cmd=${1%%$'\n'}
  return 1
}

# zsh history write on condition
function save_command() {

  EXIT_STATUS=$?

  if is_ignore_history "$cmd"; then
    return 1
  fi

  if (( $EXIT_STATUS == 0 )); then
    print -sr -- "$cmd"
    fc -W
  fi
}

# Workaround for when the exit status of the last command
# is available in precmd not in zshaddhistory.
autoload -Uz add-zsh-hook
# zshaddhistory() is called before precmd.
add-zsh-hook precmd save_command
